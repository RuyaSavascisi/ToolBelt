plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.153'
}

version = "2.2.3"
group = "dev.gigaherz.toolbelt"

Provider<String> minecraftVersion(Transformer<String, String> mapper) {
    return project.userDevRuntime.runtimes.map(r -> r.values().first().specification.minecraftVersion).map(mapper)
}

base {
    archivesName = minecraftVersion { "ToolBelt-${it}" }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

subsystems {
    parchment {
        minecraftVersion="1.21"
        mappingsVersion="2024.07.07"
    }
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    // applies to all the run configs below
    configureEach {
        systemProperty 'forge.logging.markers', '' // 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        workingDirectory project.file('run')

        modSource project.sourceSets.main
    }

    client {
    }

    client2 {
        configure ("client")

        programArguments.addAll '--username', 'Dev2'
    }

    server {
    }

    data {
        programArguments.addAll '--mod', 'toolbelt', '--all', '--output', file('src/main/generated_resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

repositories {
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Tterrag maven"
        url "https://maven.tterrag.com/"
    }
    maven { // CURIOS
        name = "C4 maven"
        url "https://maven.theillusivec4.top/"
    }
    maven {
        name = "Dogforce Games maven"
        url 'https://www.dogforce-games.com/maven/'
    }
    maven {
        name = "Cursemaven"
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
    mavenLocal()
}

dependencies {
    implementation 'net.neoforged:neoforge:21.0.133-beta'

    compileOnly "dev.gigaherz.sewingkit:SewingKit-1.21:2.0.8"
    runtimeOnly "dev.gigaherz.sewingkit:SewingKit-1.21:2.0.8"

    compileOnly "mezz.jei:jei-1.21-common-api:19.5.0.56"
    runtimeOnly "mezz.jei:jei-1.21-common:19.5.0.56"
    compileOnly "mezz.jei:jei-1.21-neoforge-api:19.5.0.56"
    runtimeOnly "mezz.jei:jei-1.21-neoforge:19.5.0.56"

    //compileOnly "top.theillusivec4.curios:curios-neoforge:8.0.0-beta.4+1.20.6"
    //runtimeOnly "top.theillusivec4.curios:curios-neoforge:8.0.0-beta.4+1.20.6"

    //runtimeOnly fg.deobf("curse.maven:corail-tombstone-243707:3836552")
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "toolbelt",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}